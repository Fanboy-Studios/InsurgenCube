// Class Manager
alias cm_cur 0 // class edit ID
alias cm_weaps ["Knife" "USP .45" "M1014" "MP5K" "M21" "Intervention" "M16A3" "Frag" "Akimbo" "Heal" "Sword" "Bow"]
alias cm_perknames1 ["None" "Recon" "Power" "Time" "Speed" "Hand" "Light" "Score"]
alias cm_perknames2 ["None" "Recon" "Power" "Time" "Vision" "Streak" "Steady" "Healthy"]
alias cm_get [
	if (strcmp $arg1 "name") [
		if (checkalias (concatword "cm_" $cm_cur "_name")) [ result (getalias (concatword "cm_" $cm_cur "_name")) ] [ result (concat "Custom" $cm_cur) ]
	] [
		if (checkalias (concatword "cm_" $cm_cur "_" $arg1)) [ result (getalias (concatword "cm_" $cm_cur "_" $arg1)) ] [ result 0 ]
	]
]
alias cm_set [
	(concatword "cm_" $cm_cur "_" $arg1) = $arg2
]

if (! (checkalias cm_firstrun)) [
	loop i 10 [
		(concatword "cm_" $i "_name") = (concat "C" $i)
		(concatword "cm_" $i "_p") = 6
		(concatword "cm_" $i "_s") = 1
		(concatword "cm_" $i "_1") = 0
		(concatword "cm_" $i "_2") = 0
	]
	cm_firstrun = 1
]

alias classmanager_sel_prefab [
	echo (concat "Selected default class:" (getlang (concatword "m_set_class_prefab_" $arg1)))
	if (= $arg1 1) [
		nextprimary 6 // AR
		nextsecondary 1 // Pistol
		nextperk1 2 // Power
	] [
	if (= $arg1 2) [
		nextprimary 3 // SMG
		nextsecondary 1 // Pistol
		nextperk1 1 // Recon
	] [
	if (= $arg1 3) [
		nextprimary 5 // Bolt
		nextsecondary 1 // Pistol
		nextperk1 5 // Hand
		nextperk2 6 // Steady
	] [
	if (= $arg1 4) [
		nextprimary 2 // Shotgun
		nextsecondary 11 // RPG
		nextperk1 4 // Speed
		nextperk2 6 // Steady
	] [
	if (= $arg1 5) [
		nextprimary 10 // Sword
		nextsecondary 9 // Heal
		nextperk1 3 // Time
	]]]]]
]

alias classmanager_sel [
	echo (concatword "Selected Class: " (cm_get name))
	nextprimary (cm_get p)
	nextsecondary (cm_get s)
	nextperk1 (cm_get 1)
	nextperk2 (cm_get 2)
]

// Class Menu
alias showclassmenu [
	newmenu Classes
	menuclear
	menuitem (getlang m_set_class_prefab_1) 	"classmanager_sel_prefab 1"
	menuitem (getlang m_set_class_prefab_2) 	"classmanager_sel_prefab 2"
	menuitem (getlang m_set_class_prefab_3) 	"classmanager_sel_prefab 3"
	menuitem (getlang m_set_class_prefab_4) 	"classmanager_sel_prefab 4"
	menuitem (getlang m_set_class_prefab_5) 	"classmanager_sel_prefab 5"
	menuitem
	loop i 10 [
		cm_cur = $i // easy hack to use the macro
		menuitem (cm_get name) 	(concatword "cm_cur = " $i " ; showclass")
	]
	showmenu Classes
]

// Class Editor
alias showclass [
	closemenu (concatword "Edit Class #" $cm_cur)
	closemenu Primary
	closemenu Secondary
	closemenu Perks1
	closemenu Perks2
	closemenu Perks3
	newmenu (concatword "Edit Class #" $cm_cur)
	menuclear
	menuitem "        [Use this class!]" "classmanager_sel"
	menuitem
	menuitemtextinput "Name: " "result (cm_get name)" "cm_set name $arg1"
	menuitem
	menuitem (concatword "Primary: " (at $cm_weaps (cm_get p))) "showmenu Primary"
	menuitem (concatword "Secondary: " (at $cm_weaps (cm_get s))) "showmenu Secondary"
	menuitem
	if (< (cm_get 1) 4) [
		menuitem (concatword "Combined Perk: " (at $cm_perknames1 (cm_get 1))) "showmenu Perks3"
		menuitem "Perk 1" "showmenu Perks1"
		menuitem "Perk 2" "showmenu Perks2"
	] [
		menuitem "Combined Perk" "showmenu Perks3"
		menuitem (concatword "Perk 1: " (at $cm_perknames1 (cm_get 1))) "showmenu Perks1"
		menuitem (concatword "Perk 2: " (at $cm_perknames2 (cm_get 2))) "showmenu Perks2"
	]
	showmenu (concatword "Edit Class #" $cm_cur)
]

// Class Manager Helper Menus

// Weapons
newmenu Primary
menumdl weapons/assault/menu mapmodel 75 12
menuitem (getlang m_set_weap_assault) 	"cm_set p 6;showclass"	"chmenumdl Primary weapons/assault/menu mapmodel 75 12"
menuitem (getlang m_set_weap_smg) 		"cm_set p 3;showclass"	"chmenumdl Primary weapons/subgun/menu mapmodel 75 12"
menuitem (getlang m_set_weap_sniper) 	"cm_set p 4;showclass"	"chmenumdl Primary weapons/sniper/menu mapmodel 75 12"
menuitem (getlang m_set_weap_bolt) 		"cm_set p 5;showclass"	"chmenumdl Primary weapons/bolt/menu mapmodel 75 12"
menuitem (getlang m_set_weap_shotgun) 	"cm_set p 2;showclass"	"chmenumdl Primary weapons/shotgun/menu mapmodel 75 12"
menuitem (getlang m_set_weap_sword) 	"cm_set p 10;showclass"	"chmenumdl Primary weapons/sword/menu mapmodel 75 12"

newmenu Secondary
menumdl weapons/pistol/menu mapmodel 75 12
menuitem (getlang m_set_weap_pistol) 	"cm_set s 1;showclass"		"chmenumdl Secondary weapons/pistol/menu mapmodel 75 12"
menuitem (getlang m_set_weap_heal) 		"cm_set s 9;showclass"		"chmenumdl Secondary weapons/heal/menu mapmodel 75 12"
menuitem (getlang m_set_weap_bow) 		"cm_set s 11;showclass"		"chmenumdl Secondary weapons/bow/menu mapmodel 75 12"

newmenu Perks3
menuitemimage "packages/misc/perks/none.png" 		(getlang m_set_perk_none) 		"cm_set 1 0;cm_set 2 0;showclass"
menuitemimage "packages/misc/perks/radar.png" 		(getlang m_set_perk_radar) 		"cm_set 1 1;showclass"
menuitemimage "packages/misc/perks/power.png" 		(getlang m_set_perk_power) 		"cm_set 1 2;showclass"
menuitemimage "packages/misc/perks/time.png" 		(getlang m_set_perk_time) 		"cm_set 1 3;showclass"

newmenu Perks1
menuitemimage "packages/misc/perks/none.png" 		(getlang m_set_perk_none) 		"cm_set 1 0;showclass"
menuitemimage "packages/misc/perks/speed.png" 		(getlang m_set_perk_speed) 		"cm_set 1 4;showclass"
menuitemimage "packages/misc/perks/hand.png" 		(getlang m_set_perk_hand) 		"cm_set 1 5;showclass"
menuitemimage "packages/misc/perks/light.png" 		(getlang m_set_perk_light) 		"cm_set 1 6;showclass"
menuitemimage "packages/misc/perks/point.png" 		(getlang m_set_perk_point) 		"cm_set 1 7;showclass"

newmenu Perks2
menuitemimage "packages/misc/perks/none.png" 		(getlang m_set_perk_none) 		"cm_set 2 0;showclass"
menuitemimage "packages/misc/perks/vision.png" 		(getlang m_set_perk_vision) 	"cm_set 2 4;showclass"
menuitemimage "packages/misc/perks/streak.png" 		(getlang m_set_perk_streak) 	"cm_set 2 5;showclass"
menuitemimage "packages/misc/perks/steady.png" 		(getlang m_set_perk_steady) 	"cm_set 2 6;showclass"
menuitemimage "packages/misc/perks/health.png" 		(getlang m_set_perk_health) 	"cm_set 2 7;showclass"